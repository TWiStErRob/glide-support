plugins {
	// TODO AGP 7.4 / 8.0, make all warnings fail in gradle.properties https://issuetracker.google.com/issues/264177800
	id("com.android.application") version "7.3.1"
}

repositories {
	mavenCentral()
	google()
	// https://github.com/bumptech/glide/wiki/Snapshots#building-snapshots-locally
	if (project.hasProperty("local.repo")) {
		maven { name = "glide-local"; url = project.property("local.repo") }
	}
	// https://github.com/bumptech/glide/wiki/Snapshots#obtaining-snapshots
	maven { name = "glide-snapshot"; url = "https://oss.sonatype.org/content/repositories/snapshots" }
}

android {
	compileSdkVersion = 33
	namespace = "com.bumptech.glide.supportapp"
	defaultConfig {
		//noinspection MinSdkTooLow holding back for when AndroidX is migrated.
		minSdkVersion 14
		//noinspection ExpiredTargetSdkVersion holding back for when AndroidX is migrated.
		targetSdkVersion 28
		versionCode = 1
		versionName = "0.1"
		multiDexEnabled = true
		vectorDrawables.useSupportLibrary = true
		proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
	}
	sourceSets.main.java.srcDir 'src/main/thirdparty'
	productFlavors {
		glide3 {
			flavorDimensions 'default'
			applicationId = "com.bumptech.glide.supportapp.v3"
			sourceSets.glide3.java.srcDir 'src/glide3/thirdparty'
			sourceSets.glide3.java.srcDir 'src/glide3/hacks'
		}
		glide4 {
			flavorDimensions 'default'
			applicationId = "com.bumptech.glide.supportapp.v4"
			sourceSets.glide4.java.srcDir 'src/glide4/thirdparty'
			sourceSets.glide4.java.srcDir 'src/glide4/hacks'
		}
	}

	variantFilter {variant ->
		if (variant.buildType.name == buildTypes.release.name) {
			variant.ignore = true
		}
	}

	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	packagingOptions {
		jniLibs.useLegacyPackaging = false
		resources {
			excludes += [
					"META-INF/DEPENDENCIES.txt",
					"META-INF/NOTICE",
					"META-INF/NOTICE.txt",
					"META-INF/LICENSE",
					"META-INF/LICENSE.txt",
			]
		}
	}

	lint {
		checkAllWarnings = true
		abortOnError = false
	}
}

def stethoVersion = "1.6.0"

dependencies {
	// Immediate SNAPSHOT resolution (in case the built version is too new), default is a day
	//configurations.glide4Implementation.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

	// Basic Android dependencies
	implementation("androidx.annotation:annotation:1.5.0")
	implementation("androidx.appcompat:appcompat:1.5.1")
	implementation("androidx.preference:preference:1.2.0")

	// Extra dependencies for individual issues
	implementation("androidx.cardview:cardview:1.0.0")
	implementation("androidx.recyclerview:recyclerview:1.2.1")
	implementation("androidx.palette:palette:1.0.0")
	implementation("androidx.percentlayout:percentlayout:1.0.0")
	implementation("com.google.android.material:material:1.7.0")
	implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1")
}

dependencies { // Glide v4
	def glideVersion = '4.14.2'
	//noinspection GradleDependency false positive from below, this is actually a good version.
	glide4Implementation "com.github.bumptech.glide:glide:${glideVersion}"
	glide4Implementation "com.github.bumptech.glide:annotations:${glideVersion}"
	glide4AnnotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"
	glide4Implementation "com.github.bumptech.glide:recyclerview-integration:${glideVersion}@aar"

	//noinspection AnnotationProcessorOnCompilePath APT for code navigation (DO NOT USE in production, not even for testing)
	testGlide4CompileOnly "com.github.bumptech.glide:compiler:${glideVersion}"

	// OkHttp
	//noinspection GradleDependency the warning is for v3 below.
	glide4Implementation "com.github.bumptech.glide:okhttp3-integration:${glideVersion}"
	glide4Implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	glide4Implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

	// Volley
	//noinspection GradleDependency the warning is for v3 below.
	glide4Implementation "com.github.bumptech.glide:volley-integration:${glideVersion}"
	glide4Implementation 'com.android.volley:volley:1.2.1'

	glide4Implementation("com.facebook.stetho:stetho:${stethoVersion}")
	glide4Implementation("com.facebook.stetho:stetho-okhttp3:${stethoVersion}")
	glide4Implementation("com.facebook.stetho:stetho-urlconnection:${stethoVersion}")
}

dependencies { // Glide v3
	def glideVersion = '3.9.0-SNAPSHOT'
	def glideIntegrationVersion = '1.6.0-SNAPSHOT'
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:glide:${glideVersion}"

	// OkHttp
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:okhttp-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.squareup.okhttp:okhttp:2.7.5'

	// OkHttp3
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:okhttp3-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	glide3Implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

	// Volley
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:volley-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.mcxiaoke.volley:library:1.0.19'

	// Glide Transformations
	//noinspection GradleDependency cannot upgrade because not in Maven Central, or needs Glide v4.
	glide3Implementation("jp.wasabeef:glide-transformations:1.0.6")
	//noinspection GradleDependency cannot upgrade because not in Maven Central, or needs Glide v4.
	glide3Implementation("jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.0")

	// Extra dependencies for individual issues
	glide3Implementation("com.firebase:firebase-client-android:2.5.2")

	glide3Implementation("com.facebook.stetho:stetho:${stethoVersion}")
	glide3Implementation("com.facebook.stetho:stetho-okhttp3:${stethoVersion}")
	glide3Implementation("com.facebook.stetho:stetho-urlconnection:${stethoVersion}")

	glide3Implementation("com.squareup.picasso:picasso:2.71828")
}

dependencies { // Competition
	//implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5' // not used yet
	//implementation 'com.facebook.fresco:fresco:0.9.0' // not used yet, it has too many dependencies
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += [
			"-Werror",
			"-Xlint:all",
			"-Xlint:-auxiliaryclass",
			"-Xlint:-processing",
	]
	if (it.name.contains("Glide4")) {
		options.compilerArgs += [
				// The framework in IssueInfoAdapter and GlideReset needs GlideModules at runtime.
				"-Xlint:-deprecation",
		]
		options.fork = true
		options.forkOptions.jvmArgs += [
				"--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
		]
	}
}
