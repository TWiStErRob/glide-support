buildscript {
	repositories {
		mavenCentral()
		google()
	}
	dependencies {
		classpath("com.android.tools.build:gradle:7.3.1")
	}
}
apply plugin: "com.android.application"

repositories {
	mavenCentral()
	google()
	// https://github.com/bumptech/glide/wiki/Snapshots#building-snapshots-locally
	if (project.hasProperty("local.repo")) {
		maven { name = "glide-local"; url = project.property("local.repo") }
	}
	// https://github.com/bumptech/glide/wiki/Snapshots#obtaining-snapshots
	maven { name = "glide-snapshot"; url = "https://oss.sonatype.org/content/repositories/snapshots" }
	flatDir name: 'libs', dirs: 'libs'
}

android {
	compileSdkVersion = 28
	namespace = "com.bumptech.glide.supportapp"
	defaultConfig {
		//noinspection MinSdkTooLow holding back for when AndroidX is migrated.
		minSdkVersion 14
		//noinspection ExpiredTargetSdkVersion holding back for when AndroidX is migrated.
		targetSdkVersion 28
		versionCode = 1
		versionName = "0.1"
		vectorDrawables.useSupportLibrary = true
		proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
	}
	sourceSets.main.java.srcDir 'src/main/thirdparty'
	productFlavors {
		glide3 {
			flavorDimensions 'default'
			applicationId = "com.bumptech.glide.supportapp.v3"
			sourceSets.glide3.java.srcDir 'src/glide3/thirdparty'
			sourceSets.glide3.java.srcDir 'src/glide3/hacks'
		}
		glide4 {
			flavorDimensions 'default'
			applicationId = "com.bumptech.glide.supportapp.v4"
			sourceSets.glide4.java.srcDir 'src/glide4/thirdparty'
			sourceSets.glide4.java.srcDir 'src/glide4/hacks'
		}
	}

	variantFilter {variant ->
		if (variant.buildType.name == buildTypes.release.name) {
			variant.ignore = true
		}
	}

	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_1_7
		targetCompatibility = JavaVersion.VERSION_1_7
	}

	packagingOptions {
		jniLibs.useLegacyPackaging = false
		resources {
			excludes += [
					"META-INF/DEPENDENCIES.txt",
					"META-INF/NOTICE",
					"META-INF/NOTICE.txt",
					"META-INF/LICENSE",
					"META-INF/LICENSE.txt",
			]
		}
	}

	lint {
		checkAllWarnings = true
		abortOnError = false
	}
}

dependencies {
	// Immediate SNAPSHOT resolution (in case the built version is too new), default is a day
	//configurations.glide4Implementation.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

	implementation fileTree(dir: 'libs', include: [ '*.jar' ])
	//implementation(name: 'somefile_name', ext: 'aar')
}

dependencies { // Glide v4
	def glideVersion = '4.9.0'
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide4Implementation "com.github.bumptech.glide:glide:${glideVersion}"
	glide4Implementation "com.github.bumptech.glide:annotations:${glideVersion}"
	glide4AnnotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"
	glide4Implementation "com.github.bumptech.glide:recyclerview-integration:${glideVersion}@aar"

	//noinspection AnnotationProcessorOnCompilePath APT for code navigation (DO NOT USE in production, not even for testing)
	testGlide4CompileOnly "com.github.bumptech.glide:compiler:${glideVersion}"

	// OkHttp
	//noinspection GradleDependency the warning is for v3 below.
	glide4Implementation "com.github.bumptech.glide:okhttp3-integration:${glideVersion}"
	glide4Implementation 'com.squareup.okhttp3:okhttp:4.9.3'
	glide4Implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

	// Volley
	//noinspection GradleDependency the warning is for v3 below.
	glide4Implementation "com.github.bumptech.glide:volley-integration:${glideVersion}"
	glide4Implementation 'com.android.volley:volley:1.2.1'

	// Basic Android dependencies
	glide4Implementation("androidx.annotation:annotation:1.5.0")
	glide4Implementation("androidx.legacy:legacy-support-v4:1.0.0")
	glide4Implementation("androidx.appcompat:appcompat:1.0.0")
	glide4Implementation("androidx.exifinterface:exifinterface:1.0.0")
	glide4Implementation("androidx.recyclerview:recyclerview:1.0.0")

	glide4Implementation("com.facebook.stetho:stetho:1.5.1")
	glide4Implementation("com.facebook.stetho:stetho-okhttp3:1.5.1")
	glide4Implementation("com.facebook.stetho:stetho-urlconnection:1.5.1")
}

dependencies { // Glide v3
	def glideVersion = '3.9.0-SNAPSHOT'
	def glideIntegrationVersion = '1.6.0-SNAPSHOT'
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:glide:${glideVersion}"

	// OkHttp
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:okhttp-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.squareup.okhttp:okhttp:2.7.5'

	// OkHttp3
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:okhttp3-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.squareup.okhttp3:okhttp:4.9.3'
	glide3Implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

	// Volley
	//noinspection GradleDependency latest v3, no need to upgrade to v4
	glide3Implementation "com.github.bumptech.glide:volley-integration:${glideIntegrationVersion}"
	glide3Implementation 'com.mcxiaoke.volley:library:1.0.19'

	// Glide Transformations
	//noinspection GradleDependency cannot upgrade because not in Maven Central, or needs Glide v4.
	glide3Implementation("jp.wasabeef:glide-transformations:1.0.6")
	//noinspection GradleDependency cannot upgrade because not in Maven Central, or needs Glide v4.
	glide3Implementation("jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.0")

	// Basic Android dependencies
	glide3Implementation("androidx.annotation:annotation:1.5.0")
	glide3Implementation("androidx.legacy:legacy-support-v4:1.0.0")
	glide3Implementation("androidx.appcompat:appcompat:1.0.0")

	// Extra dependencies for individual issues
	glide3Implementation("androidx.cardview:cardview:1.0.0")
	glide3Implementation("androidx.recyclerview:recyclerview:1.0.0")
	glide3Implementation("androidx.palette:palette:1.0.0")
	glide3Implementation("androidx.percentlayout:percentlayout:1.0.0")
	glide3Implementation("com.google.android.material:material:1.0.0")
	glide3Implementation("androidx.exifinterface:exifinterface:1.0.0")
	glide3Implementation("com.firebase:firebase-client-android:2.5.2")

	glide3Implementation("com.facebook.stetho:stetho:1.5.1")
	glide3Implementation("com.facebook.stetho:stetho-okhttp3:1.5.1")
	glide3Implementation("com.facebook.stetho:stetho-urlconnection:1.5.1")

	glide3Implementation("com.squareup.picasso:picasso:2.71828")
}

dependencies { // Competition
	//implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5' // not used yet
	//implementation 'com.facebook.fresco:fresco:0.9.0' // not used yet, it has too many dependencies
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += [
			"-Werror",
			"-Xlint:all",
			"-Xlint:-auxiliaryclass",
			"-Xlint:-deprecation",
			"-Xlint:-processing",
	]
}
